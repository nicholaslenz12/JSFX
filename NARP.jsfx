desc:NARP
options:gfx_hz=60

import knobs.jsfx
import shapes.jsfx
import math.jsfx
import defines.jsfx
import data_structures.jsfx
import testing.jsfx

// ----------------------------------------------------------------------------
// SLIDER DEFINITIONS
// ----------------------------------------------------------------------------
slider1:3<0,5,1{1/1,1/2, 1/4, 1/8, 1/16, 1/32}>-Rate (Synced)
slider2:1<0.01,1,0.01>-Length

// ----------------------------------------------------------------------------
// DEFAULT CHANNEL CONFIGURATION
// ----------------------------------------------------------------------------
in_pin:none
out_pin:none

// ----------------------------------------------------------------------------
// INIT
// ----------------------------------------------------------------------------
@init
__RUN_TESTS__ ? run_tests();

gfx_ext_retina = 1;

background.color_create(LIGHTORANGE.r, LIGHTORANGE.g, LIGHTORANGE.b);

rate_knob.knob_create(1/2, 1/2, 1/5, 0, 5, 3, 1);
rate_knob.font.font_create(1, "Arial", 0.1); // wrapper around gfx_setfont
rate_knob.formatter.formatter_create("1/%d", "Power", "");
rate_knob.colors_shapes.color_create(COFFEE.r, COFFEE.g, COFFEE.b);
rate_knob.colors_font.color_create(COFFEE.r, COFFEE.g, COFFEE.b);
rate_knob.colors.color_create(LIGHTORANGE.r, LIGHTORANGE.g, LIGHTORANGE.b);
rate_knob.colors_hover.color_create(LIGHTORANGE.r, LIGHTORANGE.g, LIGHTORANGE.b);

length_knob.knob_create(0.65, 0.75, 0.10, 0.01, 1, 1, 2);
length_knob.font.font_create(1, "Arial", 0.05);
length_knob.formatter.formatter_create("%0.2f", "Direct", "Length");
length_knob.colors_shapes.color_create(COFFEE.r, COFFEE.g, COFFEE.b);
length_knob.colors_font.color_create(COFFEE.r, COFFEE.g, COFFEE.b);
length_knob.colors.color_create(SOFTORANGE.r, SOFTORANGE.g, SOFTORANGE.b);
length_knob.colors_hover.color_create(GORANGE.r, GORANGE.g, GORANGE.b);

t_beat = 0;

prev_note_ons = 0;
note_on_count = 1;

note_list = 0; // stores binary for key pressed, native reaper arp uses velocities
current_note_idx = -1;
playing_note = 0;

// ----------------------------------------------------------------------------
// SLIDER
// ----------------------------------------------------------------------------
@slider
rate_knob.knob_detect_slider();
length_knob.knob_detect_slider();

// ----------------------------------------------------------------------------
// BLOCK
// ----------------------------------------------------------------------------
@block
prev_list_len = note_list.length;
while (midirecv(offset, cmd, note, vel)) (
    cmd == NOTE_ON && vel > 0 ? ( // Add note to the note list
        !note_list.contains(note) ? (
            note_list.push(note);
        );
    ) : (cmd == NOTE_OFF || cmd == NOTE_ON) ? ( // Remove note from note list, (kill note if being played)
        note_list.contains(note) ? (
            midisend(offset, NOTE_OFF, note);
            current_note = NONE;
        );
        note_list.remove(note);
    ) : ( // passthrough other events
        midisend(offset, cmd, key, vel);
    );
);

// kill any dangling notes (risk of FLP error?)
note_list.length == 0 && current_note != NONE ? (
    midisend(0, NOTE_OFF, current_note);
    current_note = NONE;
);

// can we do this elsewhere?
beat_fraction = 2 ^ -slider(rate_knob.slider_idx);
cycle_rate = 1/srate * tempo/60 / beat_fraction / 4;
sample_position = 0;

cycle_length = srate * 60/tempo * beat_fraction * 4;

// Just starting, so start playout of next note immediately
note_list.length > 0 && prev_list_len == 0 ? (
    t_beat = cycle_length;
    prev_note_ons = 1;
);

// ----------------------------------------------------------------------------
// SAMPLE
// ----------------------------------------------------------------------------
@sample
t_beat += 1;

note_list.length > 0 && t_beat >= cycle_length && !playing_note ? (
    t_beat -= cycle_length;
    current_note = note_list.find_larger_cyclic(current_note);
    midisend(sample_position, NOTE_ON, current_note, 127);
    playing_note = 1;
);

note_list.length > 0 && t_beat >= slider(length_knob.slider_idx) * cycle_length && playing_note ? (
    midisend(sample_position, NOTE_OFF, current_note, 127);
    playing_note = 0;
);

sample_position += 1;
// ----------------------------------------------------------------------------
// GRAPHICS
// ----------------------------------------------------------------------------
@gfx 624 395

background.gfx_colors_from_color();
gfx_rect(0, 0, gfx_w, gfx_h);

rate_knob.knob_draw();
rate_knob.knob_detect_reset();
rate_knob.knob_detect_drag();
rate_knob.knob_update_slider(1);
rate_knob.knob_draw_arrows();
// sliderchange(slider1);

length_knob.knob_draw();
length_knob.knob_detect_reset();
length_knob.knob_detect_drag();
length_knob.knob_update_slider(0.01);

__PROG_STATE__   != GOOD ? gfx_drawstr("BAD PROG STATE");
__TEST_RESULTS__ != GOOD ? gfx_drawstr("TEST REGRESSION");
