desc:Data Structures

import defines.jsfx

@init
// Array-like

// Need this or just find()?
function contains(val) local(n) (
    result = 0;
    n = 0;
    loop(this.length, // use while to break early?
        this[n] == val ? result = 1;
        n += 1;
    );
    
    result;
);

function find(val) local(n) (
    result = NOT_FOUND;
    n = 0;
    loop(this.length, // use while to break early?
        this[n] == val ? result = n;
        n += 1;
    );
    
    result;
);

function push(val) (
    this[this.length] = val;
    this.length += 1;
);

function remove_at(idx) local(n, num_move) (
    num_move = this.length - idx - 1;
    n = 0;
    loop(num_move,
        this[idx + n] = this[idx + n + 1];
        n += 1;
    );
    this.length -= 1;
);

function remove(val) local(n, idx) (
    idx = this.find(val);
    !(idx == NOT_FOUND) ? (
        this.remove_at(idx);
    );
);

function sort() local(sorted, n, temp, flips) (
    sorted = this.length < 2;
    while (!sorted) (
        flips = 0;
        n = 0;
        loop(this.length-1,
            this[n] > this[n+1] ? (
                temp = this[n+1];
                this[n+1] = this[n];
                this[n] = temp;
                flips += 1;
            );
            n += 1;
        );
        sorted = flips == 0;
    );
);

function min() local(i, m, n) (
    m = this[0];

    i = 0;
    n = 1;
    loop(this.length-1,
        this[n] < m ? (
            m = this[n];
            i = n;
        );
        n += 1;
    );

    m;
);

function end() (
    this + max(this.length - 1, 0);
);

function clear() (
    this.length = 0;
);

// Alternative to using this is just to keep a sorted version that we generate
// during the MIDI receive block
// Be careful since this is not thread-safe with respect to `this`
// This is a bit complicated
function find_larger_cyclic(val) local(n, ret, idx) (
    n = 0;
    larger_than = this.end() + 1; // align memory to just after `this`

    loop(this.length,
        this[n] > val ? (
            larger_than.push(this[n]);
        );
        n += 1;
    );

    ret = larger_than.length == 0 ? this.min() : larger_than.min();
    larger_than.clear();

    ret;
);